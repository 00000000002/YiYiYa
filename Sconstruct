# coding:utf-8
# *******************************************************************
# * Copyright 2021-2080 evilbinary
# * 作者: evilbinary on 01/01/20
# * 邮箱: rootdebug@163.com
# ********************************************************************
import os
import math
import platform 
import sys
plt = platform.system()

# board select platform 选择你要编译的平台
# platform='pc'
# platform='v3s'
# platform='raspi2'
# platform='esp32'
platform='stm32f4xx'

supports_archs={
    'arm': ['armv5','armv6','armv7','armv7-a'],
    'x86': ['i386','x86_64'],
    'xtensa':  ['lx6']
}

support_platform={
    'stm32f4xx':'armv7',
    'pc':'i386',
    'raspi2':'armv7-a',
    'esp32':'xl6',
    'v3s':'armv7-a'
}

def get_arch(arch):
    archs={}
    for k,v in supports_archs.items():
        for i in v:
            archs[i]=k
    return archs[arch]   

arch=support_platform[platform]
arch_type=get_arch(arch)
archs=supports_archs[arch_type]

print('welcome to yiyiya os build')
print('your select platform: %s arch: %s  support archs: %s'%(platform,arch, archs))

DefaultEnvironment(tools=[])
env = Environment(ENV = {}, tools=['gcc'])

cc_path='/opt/local/bin/'
cc_prefix='i386-elf-'
cc_lib_path='' #or /opt/local/lib/gcc/arm-none-eabi/9.2.0/ /lib/gcc/arm-none-eabi/9.2.1/


if plt=='Linux':
    cc_path='/usr/bin/'
    cc_prefix=''
elif plt=='Darwin':
    pass
elif plt=='Windows':
    cc_path=''
    cc_prefix=''
    cc_lib_path='D:\\Program Files\\gcc-arm-none-eabi-9\\lib\\gcc\\arm-none-eabi\\9.2.1\\'
    pass
else:
    print('no support ^_^! '+plt)

if arch_type == 'arm':
    cc_prefix='arm-none-eabi-'
elif arch_type=='xtensa':
    cc_prefix='xtensa-esp32-elf-'
    cc_path='~/.espressif/tools/xtensa-esp32-elf/esp-2021r1-8.4.0/xtensa-esp32-elf/bin/'

gcc= cc_path+cc_prefix
env = Environment(
        ENV=os.environ,
        APP=True,
        CC = gcc+'gcc',
        LD=gcc+'ld',
        CXX=gcc+'g++',
        AR= gcc+'ar',
        STRIP=gcc+"strip",
        RANLIB=gcc+'ranlib',
        OBJCOPY=gcc+'objcopy',
        AS=gcc+'as',
        CFLAGS='-DDUCK -DX86 -march=i486 -I. -I./include -Ilibs/include -g -nostdlib -nostdinc  -fno-builtin -m32 -c -std=c99 -std=gnu99 -w -D'+platform.upper(),
        #PATH= os.environ['PATH'],
        LIBPATH=['.','../arch/','../driver','../libs/'],
        LINKFLAGS='-nostdlib -nostartfiles -e start  -g ',
        OBJCOPYFLAGS='-S -g',
        ARCH=arch,
        PLATFORM=platform,
        LINKLD='xlinker/link.ld',
        MYLIB=None,
        LIBC=['../../libs/libc/libc.a','../../libs/libalgorithm/libalgorithm.a'],
        LIBCFLAGS='-Ilibs/include/c/ -DLIBYC',
        # LIBC=['../../app/libmusl/lib/libc.a',],#'/opt/local/lib/gcc/i386-elf/9.2.0/libgcc.a'
        # LIBCFLAGS=' -Iapp/libmusl/include/ -Iapp/libmusl/obj/include/ -Iapp/libmusl/arch/generic/ -Iapp/libmusl/arch/arm/ ',
        USER='--entry main -Txlinker/user.ld',
        ARCHS=archs,
        ARCHTYPE=arch_type
        )

if plt=='Linux':
    if arch in archs:
        pass
    else:
        env['LINKFLAGS']= env['LINKFLAGS']+' -m32'
    env['CFLAGS']= env['CFLAGS']+' -no-pie -fno-pic -Llibgcc.a '
    #env['MYLIB']= ''
    pass
elif plt=='Windows':
    env['LINKFLAGS']='-nostdlib  ',
    env['CFLAGS']= env['CFLAGS']+' -fno-stack-protector -mno-stack-arg-probe ' #-ffreestanding -nostdlib  
    env['USER']='--entry main -Txlinker/user.ld   '
    env['MYLIB']=cc_lib_path+'libgcc.a'

if arch_type == 'arm':
    env['APP']=True
    env['USER']=' --entry main -Txlinker/user-arm.ld '
    env['CFLAGS']=' -D'+arch.upper().replace('-','_') + '  -DARM -I. -I./include  -Ilibs/include -g -c -std=c99 -std=gnu99 -w  -D'+platform.upper() # -nostdlib -nostdinc -fno-builtin -DMALLOC_TRACE -mcpu=cortex-a7  -mtune=cortex-a7 -mfpu=vfpv4 -mfloat-abi=hard -mfloat-abi=softfp

    #env['CFLAGS']= env['CFLAGS']+ ' -fno-omit-fram e-pointer -mapcs -mno-sched-prolog ' #for debug backtrace
    if platform=='raspi2': 
        env['CFLAGS']= env['CFLAGS']+ ' -mcpu=cortex-a7  -mtune=cortex-a7 -mfpu=vfpv4  -mfloat-abi=softfp '
        pass
    elif platform=='v3s':
        env['CFLAGS']= env['CFLAGS']+ ' -mcpu=cortex-a7  -mtune=cortex-a7 -mfpu=vfpv4  -mfloat-abi=softfp '
        env['USER']='--entry main -Txlinker/user-v3s.ld'
        env['LINKLD']='xlinker/link-v3s.ld'
    elif platform =='stm32f4xx':
        env['APP']=False
        env['CFLAGS']= env['CFLAGS']+ ' -specs=nosys.specs -mcpu=cortex-m4 -nolibc -nostdlib -nostdinc -fno-builtin -DUSE_HAL_DRIVER   -mthumb -mthumb-interwork  -mfloat-abi=soft -mfpu=vfpv4-d16 -DSTM32F401xC'# -DSTM32F401xC  -Wl,--gc-sections -fdata-sections -ffunction-sections    -mfpu=fpv4-sp-d16  -mcpu=cortex-m4  -ffreestanding -nostdlib  -mfloat-abi=hard -mfpu=fpv4-sp-d16   # -mfloat-abi=hard -mfloat-abi=softfp  -mfloat-abi=softfp
        env['LINKLD']='xlinker/link-stm32f4xx.ld'
        arch='armv7e-m'
    else:
        env['LINKLD']='xlinker/link-'+platform+'.ld'
    #env['CFLAGS']= ' -march='+arch

elif arch_type=='xtensa':
    env['APP']=False
    env['CFLAGS']='-DXTENSA -I. -I./include  -Ilibs/include -g -nostdlib -nostdinc -fno-builtin -c -std=c99 -std=gnu99 -w  -D'+platform.upper()
    env['LINKLD']='xlinker/link-esp32.ld'
    pass


def generate_bin(source, target, env, for_signature):
    return '$OBJCOPY %s %s %s'%(env['OBJCOPYFLAGS'],source[0], target[0])
    
env.Append(BUILDERS={
    'Objcopy': Builder(
               generator=generate_bin,
               suffix='',)})

def get_kernel_size(env,target,source):
    if source:
        file_name=str(source[0])
        file_out=str(target[0])
        if os.path.exists(file_name):
            size=os.stat(file_name).st_size
            env['KERNEL_SIZE'] = str(size)
            env['KERNEL_BLOCK_SIZE'] = str(int(math.ceil(size/1024.0)))
            print('kernel size:',env.get('KERNEL_SIZE'))
        else :
            print('file %s not exist'%(file_name))
    else:
      env['KERNEL_SIZE'] = str(1024)
      env['KERNEL_BLOCK_SIZE'] = str(int(math.ceil(1024/1024.0)))
    return ''

def generate_kernel_size(env,target,source):
    file_out=str(target[0])
    get_kernel_size(env,target,source)
    kernel_header=file_out.upper().rsplit('/',1)[-1].replace('.','_')
    f = open(file_out, 'w')
    content="""#ifndef %s
#define %s
#define KERNEL_BLOCK_SIZE %s
#define KERNEL_SIZE %s
#endif
"""%(kernel_header,
    kernel_header,
    env.get('KERNEL_BLOCK_SIZE'),
    env.get('KERNEL_SIZE'))
    f.write(content)
    f.close()
    return ''

env.Append(BUILDERS={
    'GenerateKernelSize': Builder(
               action=generate_kernel_size
               ),
    'KernelSize': get_kernel_size
               })

Export('env')

SConscript(dirs=['libs/libalgorithm'], exports='env')
SConscript(dirs=['platform'], exports='env')
SConscript(dirs=['arch'], exports='env')
SConscript(dirs=['kernel'], exports='env')
SConscript(dirs=['libs/libkernel'], exports='env')
SConscript(dirs=['drivers'], exports='env')
kernel=SConscript(dirs=['init'], exports='env')

if arch_type in 'arm':
    boot=SConscript(dirs=['boot/arm'], exports='env')
else:
    boot=SConscript(dirs=['boot/'+arch], exports='env')

Depends(boot, kernel)

image=SConscript(dirs=['image'], exports='env')
Depends(image, boot)
Depends(image, kernel)



SConscript(dirs=['libs/libc'], exports='env')
app=SConscript(dirs=['app'], exports='env')



